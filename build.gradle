plugins {
    id 'java'

    id 'com.github.johnrengelman.shadow' version '5.0.0'
    id 'net.minecrell.plugin-yml.bukkit' version '0.3.0'
}

group 'com.nextplugins'
version '1.2.8'

tasks.build.dependsOn('shadowJar')

repositories {
    mavenCentral()

    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/groups/public/' }

    maven { url = 'https://repo.codemc.org/repository/maven-public' }
    maven { url = 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url = 'https://repo.nickuc.com/maven2/' }

    maven { url = 'https://jitpack.io/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.PlaceholderAPI:PlaceholderAPI:2.10.9'
    compileOnly 'com.gmail.filoghost.holographicdisplays:holographicdisplays-api:2.4.0'
    compileOnly 'com.nickuc.chat:nchat-api:4.2'

    implementation 'com.github.juliarn:npc-lib:2.6-RELEASE'
    implementation 'com.github.Yuhtin:update-checker:1.2'
    implementation 'com.github.HenryFabio:inventory-api:2.0.3'
    implementation 'com.github.HenryFabio:sql-provider:9561f20fd2'
    implementation 'de.tr7zw:item-nbt-api:2.10.0'

    def lombok = "org.projectlombok:lombok:1.18.24"
    compileOnly lombok
    annotationProcessor lombok
}

bukkit {
    main = 'com.nextplugins.onlinetime.NextOnlineTime'
    authors = ['Henry_Fabio', 'Eike', 'Gabriel Santos', 'Yuhtin']
    website = 'https://github.com/NextPlugins'
    version = "${project.version}"
    apiVersion = "1.13"
    load = 'POSTWORLD'
    softDepend = ['PlaceholderAPI', 'ProtocolLib', 'HolographicDisplays']

    commands {
        tempo {
            aliases = ['tempoonline']
        }
    }
}

shadowJar {
    archiveName("${project.name}-${project.version}.jar")

    relocate 'com.yuhtin.updatechecker', 'com.nextplugins.onlinetime.libs.updatechecker'
    relocate 'com.henryfabio.minecraft.inventoryapi', 'com.nextplugins.onlinetime.libs.inventoryapi'

    relocate 'com.henryfabio.sqlprovider', 'com.nextplugins.onlinetime.libs.sqlprovider'
    relocate 'com.zaxxer.hikari', 'com.nextplugins.onlinetime.libs.hikari'

    relocate 'de.tr7zw.changeme.nbtapi', 'com.nextplugins.onlinetime.libs.nbtapi'
    relocate 'de.tr7zw.annotations', 'com.nextplugins.onlinetime.libs.annotations'
}

compileJava {
    options.encoding = "UTF-8"

    sourceCompatibility = "11"
    targetCompatibility = "11"
}
